CoreASM TerminationDetection

use Standard
use StandardPlugins
use Signature
use MathPlugin

enum COLOR = {black, white}
enum TOKEN = {blackToken, whiteToken}
enum STATUS = {active, passive}
enum MACHINE = {m0, m1, m2} 
enum COMPUTATION = {c1,c2}
universe Agents = {protocol, activation, p, supervisor}
function color: MACHINE * COMPUTATION -> COLOR
function token: MACHINE * COMPUTATION -> TOKEN
function status: MACHINE * COMPUTATION -> STATUS
                
init InitRule

rule InitRule = 
	par
		forall c in COMPUTATION do{
			forall m in MACHINE do{
				color(m,c) := white
				if random() > 0.5 then status(m,c) := passive else status(m,c) := active
				//status(m,c) := passive
				if m = m0 then		
					token(m,c) := blackToken 
				else token(m,c) := undef
			}
		}
		//initilize agents
		program(protocol) := @RunStimulation
		program(supervisor) := @RunSupervisor
		program(activation) := @ChangeStatus
			
    		Agents(self) := false
	endpar

rule ForwardToken(t, m, c) = { 
		if m = m0 then token(m1, c) := t
		if m = m1 then token(m2, c) := t 
		if m = m2 then token(m0, c) := t 
}

rule RunStimulation = { 
	forall m in MACHINE do 
		forall c in COMPUTATION do
			RegularMachineProgram(m,c)

}
rule RunSupervisor = { 
	forall c in COMPUTATION do
		SupervisorMachineProgram(m0, c)
}
rule InitializeToken(m, c)= {
	token(m,c) := undef
	color(m,c) := white
}

rule RegularMachineProgram(m,c) ={

	if status(m,c) = passive and token(m,c) != undef then
	{
		seq
			if color(m,c) = black then
			{	
				print "regularMachine: color= " + color(m,c) + " status= " +status(m,c) + " token= " + token(m,c) + " machine: " + m + " computation: " + c   	
				ForwardToken(blackToken, m, c)
				if token(m,c) = whiteToken then color(m,c) := white
			}
			else
			{
				print "regularMachine: color= " + color(m,c) + " status= " +status(m,c) + " token= " + token(m,c) + " machine: " + m + " computation: " + c   	
				ForwardToken(token(m,c), m, c)
			}
 			InitializeToken(m,c)
	}
}

rule SendMessage(m,c) = {
	if status(m, c) = active then
		color(m, c) := black
		choose receiver in MACHINE do
			status(receiver, c) := active 
	print "message sent"
}

rule SupervisorMachineProgram(m,c) ={

	if status(m,c) = passive and token(m,c) != undef then
	{	
		print "supervisorMachine: color= " + color(m,c) + " status= " +status(m,c) + " token= " + token(m,c) + " machine: " + m + " computation: " + c
		if color(m,c) = white and token(m,c) = whiteToken then
				ReportGlobalTermination
		else 
		{
			seq
				ForwardToken(whiteToken, m, c)
				InitializeToken(m,c)
		}
	}
} 
    	
rule ChangeStatus ={
	if random() < 0.1 then{
	choose m in MACHINE do{ 
		choose c in COMPUTATION do{
			seq 
				SendMessage(m,c)
				makePassive
		}
	}
	}
}
	
rule makePassive ={
	print "makepassive"
	choose m in MACHINE do
		choose c in COMPUTATION do
			status(m,c) := passive
}
rule ReportGlobalTermination =
{
		print "******************terminated******************"
		program(self) := undef
		program(protocol) := undef
		program(supervisor) := undef
		program(activation) := undef
}

